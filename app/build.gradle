apply plugin: 'com.android.application'
apply plugin: 'com.chenenyu.router'
apply from:"../config.gradle"
def channel_name = project.hasProperty("CNAME") ? "${CNAME}" : ""
android {
    compileSdk rootProject.ext.compileSdk
    namespace 'com.fuan.chameleon'
    defaultConfig {
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdkVer
        multiDexEnabled true
        applicationId "com.hkbyte.chameleon"
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        def name = "".equals(channel_name) ? "C100" : channel_name
        buildConfigField 'String', 'CNAME', "\"${name}\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }



    signingConfigs {
        releasehk {
            // 正式版本
            storeFile file('../sign/hk')
            storePassword signature.KEYSTORE_PASSWORD
            keyPassword signature.KET_PASSWORD
            keyAlias signature.KEY_ALIAS
        }

        sharehk {
            // 分享版本 (可以切换其它签名)
            storeFile file('../sign/hk')
            storePassword signature.KEYSTORE_PASSWORD
            keyPassword signature.KET_PASSWORD
            keyAlias signature.KEY_ALIAS
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    flavorDimensions "short"

    productFlavors {
        releasehk {
            applicationId "com.hkbyte.chameleon"
        }

        sharehk {
            applicationId "com.hkbyte.chameleon"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            productFlavors.releasehk.signingConfig signingConfigs.releasehk
            productFlavors.sharehk.signingConfig signingConfigs.sharehk
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            shrinkResources false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    android.applicationVariants.all { variant ->
        // assign different version code for each output
        def buildName = "";
        variant.productFlavors.each { product ->
            buildName = product.name //获取渠道名字
        }

        def type = variant.buildType.name
        variant.outputs.each { output ->
            def name = buildName
            def cname = channel_name;
            def time = new Date().format("yyyyMMddhhmmss")
            if ("".equals(cname)) {
                output.outputFileName = name + "_" + variant.versionName + "_" + type + "_" + time +".apk"
            } else {
                output.outputFileName = name + "_" +cname + "_"+ variant.versionName + "_" + type + "_" + time +".apk"
            }

            def outputFileDir = "${project.rootDir.absoluteFile}/outputs/${variant.buildType.name}"
            variant.getAssembleProvider().get().doLast {
                File out = new File(outputFileDir);
                copy {
                    variant.outputs.forEach { file ->
                        copy {
                            from file.outputFile
                            into out
                        }
                    }
                }
            }
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'

    if (zbaseMode.isLocalMode) {
        implementation project(path: ':home')
        implementation project(path: ':user')
        implementation project(path: ':zbase')
    } else {
        if (userMode.isLocalMode) {
            implementation project(path: ':user')
        } else {
            implementation 'com.github.gjb-cn.basis:user:' + cloudCode
        }

        if (homeMode.isLocalMode) {
            implementation project(path: ':home')
        } else {
            implementation 'com.github.gjb-cn.basis:home:' + cloudCode
        }
        implementation 'com.github.gjb-cn.basis:zbase:' + cloudCode
    }

}